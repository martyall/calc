integer = @{ ASCII_DIGIT+ }

bool = { "true" | "false" }

identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

primary = _{ integer | bool | identifier | "(" ~ expression ~ ")" }

unary_minus = { "-" }

atom = _{ unary_minus? ~ primary }

bin_op = _{ add | sub | mul | pow | and | or | eq}
    add = { "+" }
    sub = { "-" }
    mul = { "*" }
    pow = { "^" }
    and = { "&&" }
    or = { "||" }
    eq = { "==" }

expression = { if_then_else | atom ~ (bin_op ~ atom)* }

if_then_else = { "if" ~ expression ~ "then" ~ expression ~ "else" ~ expression }

ty = { ASCII_ALPHA+ }

typed_identifier = { identifier ~ ":" ~ ty }

public_var = { "pub" ~ typed_identifier ~ ";" }

assignment = { "let" ~ identifier ~ "=" ~ expression ~ ";"}

decls = { public_var+ ~ assignment* }

program = _{ SOI ~ decls ~ expression ~ EOI }

NEWLINE = _{ "\r\n" | "\n" }
WHITESPACE = _{ " " | "\t" | NEWLINE }